import java.time.ZoneId
import java.time.format.DateTimeFormatter
import java.time.format.DateTimeFormatterBuilder
import java.time.format.TextStyle

import static java.time.ZonedDateTime.now
import static java.time.format.DateTimeFormatter.ofLocalizedDate
import static java.time.format.FormatStyle.LONG

// TODO: Refer to https://mrhaki.blogspot.com/2016/01/groovy-goodness-customise-groovydoc.html

// Potential command-line:
//
// groovydoc -d docs -nomainforscripts -windowtitle 'Jenkins Pipeline Scripts' -fileEncoding 'UTF-8' --verbose vars/*.groovy
//

buildscript {
	dependencies {
		localGroovy()
	}
}

plugins {
	id "groovy"
}

repositories {
	jcenter()
}

dependencies {
	// Latest stable release of Groovy - pity we can't just inherit from Gradle
	// C.f. https://stackoverflow.com/a/49120662
	implementation 'org.codehaus.groovy:groovy-all:2.5.7'
}


// This file is based on https://github.com/mrhaki/Groovy-Goodness/blob/5024bd3a5a2eb96cdf072e6144412a81416a0936/Blog/Posts/Customise_Groovydoc_Output_With_Gradle/sample.groovy

// Here we add a new SourceSet
// that has a resources directory
// at 'src/templates'. This directory
// contains Groovydoc template files
// we want to override for our project.
//
// The original source files in the 'src/templates' directory may be found at
// https://github.com/apache/groovy/tree/61ee9c9fa31cef2ad/subprojects/groovy-groovydoc/src/main/resources/org/codehaus/groovy/tools/groovydoc/gstringTemplates
//
sourceSets {
	templates {
		resources {
			srcDir 'src/templates'
		}
	}
}

// This DSL method was added for us by the 'groovy' plugin
groovydoc {

	// These names might need a little work, but they should do for a first release
	docTitle = "Quick-Start Files"
	windowTitle = "The Unofficial Jenkins Pipeline Library"

	source = project.file('vars')

	// Change classpath property to include
	// the output of the SourceSet 'templates'.
	//
	// The output contains any changed stylesheet
	// or template file and must be first in the classpath,
	// BEFORE the files that are bundled with Groovy.
	classpath = sourceSets.templates.output + classpath
	destinationDir = project.file('docs')

	// TODO: Set up all the GroovyDoc task properties

	// Make sure this task always runs. Gradle's default behavior is so annoying.
	outputs.upToDateWhen { false }

	doFirst {
		DateTimeFormatter theDate = new DateTimeFormatterBuilder()
				.append(ofLocalizedDate(LONG))
				.appendLiteral(' ')
				.appendZoneText(TextStyle.SHORT)
				.toFormatter()

		// Set custom footer for generated documentation.
		footer = """\
        <div class="custom-footer">
            Generated on: ${theDate.format(now(ZoneId.of('America/New_York')))}
        </div>""".stripIndent()
	}

}


